plugins {
    id 'maven-publish'
    id "kotlin"
    id "com.google.protobuf" version "0.9.1"
    id "com.vanniktech.maven.publish"
    id "idea"
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.21.9"
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.50.2'
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.plugins {
                grpc {}
            }
            task.builtins {
                kotlin {}
            }
        }
    }
}

compileKotlin {
    dependsOn generateProto
}

kotlin.sourceSets.all {
    // Prevent build warnings for grpc's generated opt-in code
    languageSettings.optIn("kotlin.RequiresOptIn")
}

sourceSets {
    main {
        java {
            srcDirs += 'build/generated/source/proto/main/grpc'
            srcDirs += 'build/generated/source/proto/main/java'
            srcDirs += 'build/generated/source/proto/main/kotlin'
        }
    }
}

dependencies {
    implementation project(':maestro-utils')
    implementation project(':maestro-xcuitest-driver')
    implementation 'io.grpc:grpc-kotlin-stub:1.3.0'
    implementation 'io.grpc:grpc-stub:1.50.2'
    implementation 'io.grpc:grpc-protobuf:1.50.2'
    implementation 'io.grpc:grpc-okhttp:1.50.2'
    implementation 'com.google.protobuf:protobuf-kotlin:3.21.9'
    implementation 'com.michael-bull.kotlin-result:kotlin-result:1.1.14'
    implementation 'org.slf4j:slf4j-simple:1.7.36'
    implementation 'com.squareup.okio:okio:3.2.0'
    api 'com.google.code.gson:gson:2.9.0'
    api "com.squareup.okhttp3:okhttp:4.10.0"
    api 'net.harawata:appdirs:1.2.1'
    api 'com.fasterxml.jackson.module:jackson-module-kotlin:2.13.4'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.0'
    testImplementation "com.google.truth:truth:1.1.3"
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

plugins.withId("com.vanniktech.maven.publish") {
    mavenPublish {
        sonatypeHost = "S01"
    }
}

test {
    useJUnitPlatform()
}
