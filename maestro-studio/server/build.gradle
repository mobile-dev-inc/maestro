plugins {
    id 'maven-publish'
    id "kotlin"
    id 'application'
    id "com.vanniktech.maven.publish"
}

application {
    applicationName = 'maestro-studio'
    mainClassName = 'maestro.studio.ServerKt'
}

plugins.withId("com.vanniktech.maven.publish") {
    mavenPublish {
        sonatypeHost = "S01"
    }
}

def copyWebFiles = tasks.register("copyWebFiles", Copy.class) {
    dependsOn(':maestro-studio:web:build')
    from(new File(project(':maestro-studio:web').projectDir, 'build'))
    into(new File(projectDir, 'src/main/resources/web'))
}

tasks.compileJava {
    dependsOn(copyWebFiles)
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

dependencies {
    implementation(project(":maestro-orchestra"))
    implementation(project(":maestro-client"))
    implementation(project(":maestro-utils"))
    implementation platform("org.http4k:http4k-bom:4.34.2.0")
    implementation(libs.ktor.server.core)
    implementation(libs.ktor.server.netty)
    implementation(libs.ktor.server.cors)
    implementation(libs.ktor.server.status.pages)
    implementation(libs.jackson.module.kotlin)
    implementation(libs.square.okhttp)
}